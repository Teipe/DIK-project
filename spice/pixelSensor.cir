* Pixel sensor
**********************************************************************
**        Copyright (c) 2021 Carsten Wulff Software, Norway
** *******************************************************************
** Created       : wulff at 2021-7-22
** *******************************************************************
**  The MIT License (MIT)
**
**  Permission is hereby granted, free of charge, to any person obtaining a copy
**  of this software and associated documentation files (the "Software"), to deal
**  in the Software without restriction, including without limitation the rights
**  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
**  copies of the Software, and to permit persons to whom the Software is
**  furnished to do so, subject to the following conditions:
**
**  The above copyright notice and this permission notice shall be included in all
**  copies or substantial portions of the Software.
**
**  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
**  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
**  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
**  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
**  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
**  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
**  SOFTWARE.
**
**********************************************************************

*----------------------------------------------------------------
* Include
*----------------------------------------------------------------
.include ../../models/ptm_130_ngspice.spi

.param p_wp = 2.56
.param length = 0.3u
.param width = 0.1u
.param wt = 0.1u

.SUBCKT PIXEL_SENSOR VBN1 VRAMP VRESET ERASE EXPOSE READ
+ DATA_7 DATA_6 DATA_5 DATA_4 DATA_3 DATA_2 DATA_1 DATA_0 VDD VSS


XS1 VRESET VSTORE ERASE EXPOSE VDD VSS SENSOR

XC1 VCMP_OUT VSTORE VRAMP VBN1 VDD VSS COMP

* XC1 VCMP_OUT VSTORE VRAMP VDD VSS COMP_SIM

XM1 READ VCMP_OUT DATA_7 DATA_6 DATA_5 DATA_4 DATA_3 DATA_2 DATA_1 DATA_0 VDD VSS MEMORY

.ENDS

.SUBCKT MEMORY READ VCMP_OUT
+ DATA_7 DATA_6 DATA_5 DATA_4 DATA_3 DATA_2 DATA_1 DATA_0 VDD VSS

XM1 VCMP_OUT DATA_0 READ VSS MEMCELL
XM2 VCMP_OUT DATA_1 READ VSS MEMCELL
XM3 VCMP_OUT DATA_2 READ VSS MEMCELL
XM4 VCMP_OUT DATA_3 READ VSS MEMCELL
XM5 VCMP_OUT DATA_4 READ VSS MEMCELL
XM6 VCMP_OUT DATA_5 READ VSS MEMCELL
XM7 VCMP_OUT DATA_6 READ VSS MEMCELL
XM8 VCMP_OUT DATA_7 READ VSS MEMCELL

.ENDS

.SUBCKT MEMCELL CMP DATA READ VSS
M1 VG CMP DATA VSS nmos  w=0.2u  l=0.13u
M2 DATA READ DMEM VSS nmos  w=0.4u  l=0.13u
M3 DMEM VG VSS VSS nmos  w=1u  l=0.13u
C1 VG VSS 1p
.ENDS

.SUBCKT SENSOR VRESET VSTORE ERASE EXPOSE VDD VSS
* .SUBCKT SENSOR VSTORE ERASE EXPOSE VDD VSS

* Capacitor to model gate-source capacitance
C1 VSTORE VSS 100f
Rleak VSTORE VSS 100T

MN1 NODE1 VPG NODE1 NODE1 nmos W=wt L={3*wt}

MN2 NODE1 EXPOSE VSTORE VSTORE nmos W=wt L={3*wt}

MN3 VRESET ERASE VSTORE VSTORE nmos W=wt L={3*wt}

I3 0 VPG dc 0.00001n
.ENDS

.SUBCKT COMP VCMP_OUT VSTORE VRAMP VBN1 VDD VSS

MP1 NODE1 NODE1 VDD VDD pmos W={width*p_wp} L={length*p_wp}
MP2 NODE2 NODE1 VDD VDD pmos W={width*p_wp} L={length*p_wp}
MP3 NODE4 NODE2 VDD VDD pmos W={width*p_wp} L={length*p_wp}
MP4 VCMP_OUT NODE4 VDD VDD pmos W={width*p_wp} L={length*p_wp}

MN1 NODE1 VSTORE NODE3 NODE3 nmos W=width L=length
MN2 NODE3 VBN1 VSS VSS nmos W=width L=length
MN3 NODE2 VRAMP NODE3 NODE3 nmos W=width L=length
MN4 NODE4 VBN1 VSS VSS nmos W=width L=length
MN5 VCMP_OUT NODE4 VSS VSS nmos W=width L=length

.ENDS

.SUBCKT INV VIN VOUT VDD VSS

MN1 VOUT VIN VSS VSS nmos W=width L={p_wp*length}

MP1 VOUT VIN VDD VDD pmos W=width L={p_wp*length}

.ENDS

* Same circuit as COMP but with more nodes to plot
.SUBCKT COMP_DET VCMP_OUT VSTORE VRAMP VBN1 NODE1 NODE2 NODE3 NODE4 VDD VSS

MP1 NODE1 NODE1 VDD VDD pmos W={width*p_wp} L={length*p_wp} m=2
MP2 NODE2 NODE1 VDD VDD pmos W={width*p_wp} L={length*p_wp} m=2
MP3 NODE4 NODE2 VDD VDD pmos W={width*p_wp} L={length*p_wp}
MP4 VCMP_OUT NODE4 VDD VDD pmos W={width*p_wp} L={length*p_wp}

MN1 NODE1 VSTORE NODE3 NODE3 nmos W=width L=length
MN2 NODE3 VBN1 VSS VSS nmos W=width L=length
MN3 NODE2 VRAMP NODE3 NODE3 nmos W=width L=length
MN4 NODE4 VBN1 VSS VSS nmos W=width L=length
MN5 VCMP_OUT NODE4 VSS VSS nmos W=width L=length

.ENDS


